// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'

        // Android-unit-test repo
        classpath 'org.robolectric:robolectric-gradle-plugin:1.0.1'
    }
}

allprojects {
    repositories {
        maven { url "http://dl.bintray.com/populov/maven" }
        jcenter()
        mavenCentral()
        maven {
            url 'http://repo.apisense.com/apisense'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
}

def computeVersionName() {
    def date = new Date()
    def formattedDate = date.format('yy.MM.dd')
    return formattedDate
}

task updateChangelog(type:Exec) {
    commandLine 'python', 'changelogGenerator.py', 'bee-android', computeVersionName()
}

task commitNewChangelog(dependsOn: ':updateChangelog', type:Exec){
    commandLine 'git', 'commit', 'CHANGELOG.md', '-m "Automatic changelog update"'
}

task tagAndroidRelease (dependsOn: ':commitNewChangelog', type:Exec) {
    commandLine 'git', 'tag', '-a', computeVersionName(), '-m "New Release of the application"'
}

task pushNewRelease (dependsOn: ':tagAndroidRelease', type:Exec) {
    commandLine 'git', 'push', '--follow-tags'
}
