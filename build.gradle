// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.9')

        // Android-unit-test repo
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.6.3'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    apply plugin: 'artifactory'

    repositories {
        maven { url "http://dl.bintray.com/populov/maven" }
        mavenCentral()
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        contextUrl = "${artifactory_contextUrl}"
        repository {
            repoKey = 'lib-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
    resolve {
        contextUrl = "${artifactory_contextUrl}"
        repository {
            repoKey = 'apisense'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

def computeVersionName() {
    def date = new Date()
    def formattedDate = date.format('yy.MM.dd')
    return formattedDate
}

task updateChangelog(type:Exec) {
    commandLine 'python', 'changelogGenerator.py', 'bee-android', computeVersionName()
}

task commitNewChangelog(dependsOn: ':updateChangelog', type:Exec){
    commandLine 'git', 'commit', 'CHANGELOG.md', '-m "Automatic changelog update"'
}

task tagAndroidRelease (dependsOn: ':commitNewChangelog', type:Exec) {
    commandLine 'git', 'tag', '-a', computeVersionName(), '-m "New Release of the application"'
}

task pushNewRelease (dependsOn: ':tagAndroidRelease', type:Exec) {
    commandLine 'git', 'push', '--follow-tags'
}
