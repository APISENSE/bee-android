apply plugin: 'com.android.application'

// Default parameters to empty string.
if (!project.hasProperty("debug_sdk_key")) {
    ext.debug_sdk_key = ""
}

if (!project.hasProperty("release_sdk_key")) {
    ext.release_sdk_key = ""
}

if (!project.hasProperty("rollbar_key")) {
    ext.rollbar_key = "-"
}

if (!project.hasProperty("release_keystore_password")) {
    ext.release_keystore_password = ""
}

if (!project.hasProperty("release_key_password")) {
    ext.release_key_password = ""
}


dependencies {
    // Report
    compile 'com.rollbar:rollbar:0.5.3'

    // Features
    compile 'io.apisense.sdk:apisense-android-sdk:' + apisenseVersion
    compile 'io.apisense.sdk:stings-network:' + apisenseVersion
    compile 'io.apisense.sdk:stings-phone:' + apisenseVersion
    compile 'io.apisense.sdk:stings-motion:' + apisenseVersion
    compile 'io.apisense.sdk:stings-environment:' + apisenseVersion
    compile 'io.apisense.sdk:stings-visualization:' + apisenseVersion

    // Import Sensor type
    compile 'io.apisense:java-darts:1.4.2'

    // QRCode scanner
    compile 'me.dm7.barcodescanner:zbar:1.8.3'

    // Social and Gamification
    compile 'com.google.android.gms:play-services-auth:' + googleServicesVersion
    compile 'com.google.android.gms:play-services-games:' + googleServicesVersion
    compile 'com.google.android.gms:play-services-plus:' + googleServicesVersion
    compile 'com.facebook.android:facebook-android-sdk:4.15.0'

    // Graphic enhancements
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    compile 'com.romandanylyk:pageindicatorview:0.1.2'

    // Android support animation and views
    compile 'com.android.support:support-v4:' + androidSupportVersion
    compile 'com.android.support:appcompat-v7:' + androidSupportVersion
    compile 'com.android.support:recyclerview-v7:' + androidSupportVersion
    compile 'com.android.support:design:' + androidSupportVersion

    compile('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }

    // Helpers
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.apisense.bee"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode apkCode
        versionName beeVersion
        multiDexEnabled true
    }

    signingConfigs {
        customDebug {
            storeFile file("KeystoreCertificates/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        customRelease {
            storeFile file("KeystoreCertificates/release.keystore")
            storePassword "${release_keystore_password}"
            keyAlias "bee"
            keyPassword "${release_key_password}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules.debug.pro'
            signingConfig signingConfigs.customDebug
            testCoverageEnabled = true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SDK_KEY", "\"${debug_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"development"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customRelease
            buildConfigField "String", "SDK_KEY", "\"${release_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"production"'
        }
    }

    flavorDimensions "content"
    productFlavors {
        vanilla {
            dimension "content"
        }
        experimental {
            dimension "content"
            applicationIdSuffix '.experimental'
        }
    }

}

apply plugin: 'com.google.gms.google-services'
