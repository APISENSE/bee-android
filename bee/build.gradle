apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' // Used by butterknife

// Default parameters to empty string.
if (!project.hasProperty("debug_sdk_key")) {
    ext.debug_sdk_key = ""
}

if (!project.hasProperty("release_sdk_key")) {
    ext.release_sdk_key = ""
}

if (!project.hasProperty("rollbar_key")) {
    ext.rollbar_key = ""
}

if (!project.hasProperty("release_keystore_password")) {
    ext.release_keystore_password = ""
}

if (!project.hasProperty("release_key_password")) {
    ext.release_keystore_password = ""
}


dependencies {
    // Report
    compile('com.rollbar:rollbar:0.5.3')

    // Features
    compile 'io.apisense.sdk:apisense-android-sdk:1.9.0-SNAPSHOT'
    compile 'io.apisense.sdk:stings-network:1.9.0-SNAPSHOT'
    compile 'io.apisense.sdk:stings-phone:1.9.0-SNAPSHOT'
    compile 'io.apisense:java-darts:1.2.0-SNAPSHOT'
    compile 'me.dm7.barcodescanner:zbar:1.8.3' // QRCode scanner

    // Social and Gamification
    compile('com.google.android.gms:play-services-auth:9.8.0')
    compile('com.google.android.gms:play-services-games:9.8.0')
    compile('com.google.android.gms:play-services-plus:9.8.0')
    compile('com.facebook.android:facebook-android-sdk:4.15.0')

    // Graphic enhancements
    compile('com.github.PhilJay:MPAndroidChart:v2.1.6') // Draw charts

    // Android support animation and views
    compile('com.android.support:support-v4:24.2.0')
    compile('com.android.support:appcompat-v7:24.0.0')
    compile('com.android.support:recyclerview-v7:24.0.0')

    compile('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }

    // Helpers
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.apisense.bee"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 17
        versionName "1.9.0-SNAPSHOT"
    }

    signingConfigs {
        customDebug {
            storeFile file("KeystoreCertificates/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        customRelease {
            storeFile file("KeystoreCertificates/release.keystore")
            storePassword "${release_keystore_password}"
            keyAlias "bee"
            keyPassword "${release_key_password}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules.debug.pro'
            signingConfig signingConfigs.customDebug
            testCoverageEnabled = true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SDK_KEY", "\"${debug_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"development"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customRelease
            buildConfigField "String", "SDK_KEY", "\"${release_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"production"'
        }
    }
}

apply plugin: 'com.google.gms.google-services'
