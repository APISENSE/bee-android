apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'http://repo.apisense.com/apisense'
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // APISENSE dependencies
    compile('fr.inria.apisense:apisense-library:2.1.0') {
        exclude module: 'support-v4'
    }
    compile 'fr.inria.asl:asl:2.1.0@pom'
    compile 'fr.inria.asl:asl-interpreter-javascript:2.1.0'
    compile 'fr.inria.asl:asl-node-android-facade:2.1.0'

    compile('com.viewpagerindicator:library:2.4.1') {
        exclude module: 'support-v4'
    }

    compile('com.google.android.gms:play-services:6.5.87') {
        exclude module: 'support-v4'
    }

    // Tests with Robotium
    androidTestCompile('com.jayway.android.robotium:robotium-solo:5.2.1') {
        exclude module: 'support-v4'
    }

    // Android Lollipop support animation and views
    compile 'com.android.support:support-v4:21.+'
    // Sexy android-L support
    compile('com.android.support:appcompat-v7:21.0.+') {
        exclude module: 'support-v4'
    }
    compile('com.android.support:cardview-v7:21.0.+') {
        exclude module: 'support-v4'
    }
    compile('com.android.support:recyclerview-v7:21.0.+') {
        exclude module: 'support-v4'
    }

    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.github.navasmdc:MaterialDesign:1.+@aar'
    compile 'com.nineoldandroids:library:2.4.0'


}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.apisense.bee"
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 4
        versionName computeVersionName()

    }

    signingConfigs {
        customDebug {
            storeFile file("KeystoreCertificates/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customDebug
            testCoverageEnabled = true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Unit tests
// See: https://plugins.jetbrains.com/plugin/7488?pr=idea
apply plugin: 'robolectric'

dependencies {
    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.3'
}
robolectric {
    include '**/*Tests.class'
}
// Coverage
// See: http://chrisjenx.com/gradle-robolectric-jacoco-dagger/
apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}

// Merge Unit and Instrumented test execution results into one file
task mergeTestsCoverage(type: JacocoMerge, dependsOn: ["testDebug", "connectedAndroidTest"]) {
    executionData = files('build/jacoco/testDebug.exec', 'build/outputs/code-coverage/connected/coverage.ec')
    destinationFile = file('build/jacoco/mergedResults.exec')
}

// Use previous file to generate coverage over Bee main code
task testsCoverage(type: JacocoReport, dependsOn: "mergeTestsCoverage") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        xml.destination = "${buildDir}/outputs/reports/coverage/coverageReport.xml"
        html.enabled = true
        html.destination = "${buildDir}/outputs/reports/coverage/html"
    }

    // Ignoring R usage (constants only) and external libraries
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: [
                    'android/',
                    'com/apisense/bee/R*.class',
                    'com/google/',
                    'com/viewpagerindicator',
                    'net/hockeyapp/android'

            ]
    )
    sourceDirectories = files('src/main/java', 'src/main/gen')
    executionData = files('build/jacoco/mergedResults.exec')
}