apply plugin: 'com.android.application'

// Default parameters to empty string.
if (!project.hasProperty("debug_sdk_key")) {
    ext.debug_sdk_key = ""
}

if (!project.hasProperty("release_sdk_key")) {
    ext.release_sdk_key = ""
}

if (!project.hasProperty("rollbar_key")) {
    ext.rollbar_key = ""
}

if (!project.hasProperty("release_keystore_password")) {
    ext.release_keystore_password = ""
}

if (!project.hasProperty("release_key_password")) {
    ext.release_keystore_password = ""
}


dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    // Features
    compile 'com.apisense.sdk:apisense-android-sdk:1.7.0'
    compile 'me.dm7.barcodescanner:zbar:1.8.3' // QRCode scanner

    // Google play services
    compile('com.google.android.gms:play-services-games:9.0.2')
    compile('com.google.android.gms:play-services-plus:9.0.2')

    // Graphic enhancements
    compile('com.astuetz:pagerslidingtabstrip:1.0.1') // Settings page slider
    compile('com.viewpagerindicator:library:2.4.1@aar') // Slideshow's slide indicator
    compile('com.github.PhilJay:MPAndroidChart:v2.1.6') // Draw charts

    // Android support animation and views
    compile 'com.android.support:support-v4:24.0.0'
    compile('com.android.support:appcompat-v7:24.0.0')
    compile('com.android.support:cardview-v7:24.0.0')
    compile('com.android.support:recyclerview-v7:24.0.0')
    compile('com.android.support:design:24.0.0')
    compile('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.apisense.bee"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 14
        versionName "1.7.0"
    }

    signingConfigs {
        customDebug {
            storeFile file("KeystoreCertificates/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        customRelease {
            storeFile file("KeystoreCertificates/release.keystore")
            storePassword "${release_keystore_password}"
            keyAlias "bee"
            keyPassword "${release_key_password}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customDebug
            testCoverageEnabled = true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SDK_KEY", "\"${debug_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\"" // Empty init key
            buildConfigField "String", "ROLLBAR_ENV", '"development"'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customRelease
            buildConfigField "String", "SDK_KEY", "\"${release_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"production"'
        }
    }
}

apply plugin: 'com.neenbedankt.android-apt'

dependencies {
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}

apply plugin: 'com.google.gms.google-services'
