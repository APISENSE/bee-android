apply plugin: 'com.android.application'

// Default parameters to empty string.
if (!project.hasProperty("debug_sdk_key")) {
    ext.debug_sdk_key = ""
}

if (!project.hasProperty("release_sdk_key")) {
    ext.release_sdk_key = ""
}

if (!project.hasProperty("rollbar_key")) {
    ext.rollbar_key = "-"
}

if (!project.hasProperty("release_keystore_password")) {
    ext.release_keystore_password = ""
}

if (!project.hasProperty("release_key_password")) {
    ext.release_key_password = ""
}

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.apisense.bee"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode apkCode
        versionName beeVersion
        multiDexEnabled true
    }

    signingConfigs {
        customDebug {
            storeFile file("KeystoreCertificates/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        customRelease {
            storeFile file("KeystoreCertificates/release.keystore")
            storePassword "${release_keystore_password}"
            keyAlias "bee"
            keyPassword "${release_key_password}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules.debug.pro'
            signingConfig signingConfigs.customDebug
            testCoverageEnabled = true
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "String", "SDK_KEY", "\"${debug_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"development"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customRelease
            buildConfigField "String", "SDK_KEY", "\"${release_sdk_key}\""
            buildConfigField "String", "ROLLBAR_KEY", "\"${rollbar_key}\""
            buildConfigField "String", "ROLLBAR_ENV", '"production"'
        }
    }

    flavorDimensions "content"
    productFlavors {
        vanilla {
            dimension "content"
        }
        experimental {
            dimension "content"
            applicationIdSuffix '.experimental'
        }
    }

}

dependencies {
    // Report
    implementation 'com.rollbar:rollbar-java:1.0.0'

    // Support for android < 5.0, see https://developer.android.com/studio/build/multidex.html
    implementation 'com.android.support:multidex:1.0.2'

    // Report
    implementation 'com.rollbar:rollbar:0.5.3'

    // Features
    implementation('io.apisense.sdk:apisense-android-sdk:' + apisenseVersion) {
        exclude module: "com.google.firebase"
    }
    implementation "com.google.firebase:firebase-messaging:" + googleServicesVersion
    implementation 'io.apisense.sdk:stings-network:' + apisenseVersion
    implementation 'io.apisense.sdk:stings-phone:' + apisenseVersion
    implementation 'io.apisense.sdk:stings-motion:' + apisenseVersion
    implementation 'io.apisense.sdk:stings-environment:' + apisenseVersion
    implementation 'io.apisense.sdk:stings-visualization:' + apisenseVersion

    experimentalImplementation('io.apisense:netsense:1.0.2')
    experimentalImplementation("io.apisense.sdk:apisense-android-sdk:1.12.0.beta2")

    // Import Sensor type
    implementation 'io.apisense:java-darts:1.4.3'

    // QRCode scanner
    implementation 'me.dm7.barcodescanner:zbar:1.8.3'

    // Social and Gamification
    implementation 'com.google.android.gms:play-services-auth:' + googleServicesVersion
    implementation 'com.google.android.gms:play-services-games:' + googleServicesVersion
    implementation 'com.google.android.gms:play-services-plus:' + googleServicesVersion
    implementation 'com.facebook.android:facebook-android-sdk:4.15.0'

    // Graphic enhancements
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    implementation 'com.romandanylyk:pageindicatorview:0.1.2'

    // Android support animation and views
    implementation 'com.android.support:support-v4:' + androidSupportVersion
    implementation 'com.android.support:appcompat-v7:' + androidSupportVersion
    implementation 'com.android.support:recyclerview-v7:' + androidSupportVersion
    implementation 'com.android.support:design:' + androidSupportVersion

    compile('com.mikepenz:materialdrawer:5.2.9@aar') {
        transitive = true
    }

    // Helpers
    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}

apply plugin: 'com.google.gms.google-services'
